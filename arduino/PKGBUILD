# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=arduino
epoch=1
pkgver=1.0.5
pkgrel=6
pkgdesc="Arduino prototyping platform SDK"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip)
license=('GPL' 'LGPL')
depends=('avr-gcc' 'avr-libc' 'avrdude' 'libusb-compat' 'java-runtime' 'java-rxtx')
makedepends=('java-environment' 'apache-ant')
optdepends=('arduino-toolchain: stock Arduino compiler toolchain'
            'gtk2: Arduino IDE')
install="arduino.install"
source=("http://arduino.googlecode.com/files/arduino-1.0.5-src.tar.gz"
        'arduino-fix-arguments.patch'
        'arduino-ide-add-board-options.patch'
        'arduino-gcc-4.8-no-use-cxa-atexit.patch'
        'arduino.png'
        'arduino.desktop')
sha256sums=('2f222c944d317dcae40c2d9a1e8ca8c2092f1fff4e3d1814c609209ffc9afe17'
            'fd88d7e5e93c1da0680051e1c7a57ecccb289cde1db3b6834700d7d92aeed1a8'
            'e0a32a46a0cc798b3d019a675c97136817a10d5c3fe88b38df7bdfcbebb96824'
            '356f87aca8b33b97c299b21ca8dc876d446de33944409f7f8b6748b5e7772fe7'
            '3aff4239209751f0396cc27e4348f9adbd8c193e410565895811c38d2bda6ae8'
            '3991ce3980bf0bea26ad6c6836038ebece17d443e130aace901c2ac637a7746f')

prepare() {
  cd "${srcdir}/arduino-${pkgver}"

  patch -Np1 <"${srcdir}/arduino-fix-arguments.patch"
  patch -Np1 <"${srcdir}/arduino-gcc-4.8-no-use-cxa-atexit.patch"
  patch -Np1 <"${srcdir}/arduino-ide-add-board-options.patch" 
}

build() {
  cd "${srcdir}/arduino-${pkgver}/build"

  ant -Dversion=1.0.5 -Dplatform=linux dist
}

package() {
  cd "${srcdir}/arduino-${pkgver}/build/linux/work"

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/"{doc,applications,pixmaps}

  # use Arch's avr toolchain
  rm -rf hardware/tools/*

  # copy the whole SDK to /usr/share/arduino/
  cp -a . "${pkgdir}/usr/share/arduino"

  # use system's RXTX library
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial.so"
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial64.so"
  ln -sf /usr/share/java/rxtx/RXTXcomm.jar "${pkgdir}/usr/share/arduino/lib/RXTXcomm.jar"

  # at least support the FHS a little bit
  ln -s /usr/share/arduino/arduino "${pkgdir}/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "${pkgdir}/usr/share/doc/arduino"

  # desktop icon
  install -m644 "${srcdir}/arduino.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/arduino.png" "${pkgdir}/usr/share/pixmaps/"
}
